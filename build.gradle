buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.h2database:h2:1.4.199'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.6.1'
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id "com.github.spotbugs" version "3.0.0"
    id "io.freefair.lombok" version "4.1.6"
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://repo.gradle.org/gradle/libs/'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
        options.compilerArgs = [
            '-Xlint:-options',
            '-Xlint:deprecation',
            '-Xlint:unchecked',
            '-Werror'
        ]
    }

    tasks.withType(GroovyCompile) {
        options.encoding = 'utf-8'
        options.compilerArgs = ['-Xlint:-options']
        groovyOptions.encoding = 'utf-8'
    }
}

ext {
    devConfRoot = "$rootDir/modules/dev-kits/roll-dev-configs/src/main/resources/META-INF"
}

apply plugin: 'idea'
apply plugin: 'org.asciidoctor.convert'

apply from: "$devConfRoot/configs/docs.gradle"
apply from: "$devConfRoot/configs/scaffold/project.gradle"
apply from: "$devConfRoot/configs/scaffold/module.gradle"

// Need to define group and verion at root project for module scaffold use
group = 'io.github.spring-roll'
// Needs to change version in io/github/springroll/base/Version.java:13:17 TOGETHER!!
// And keep them SAME!!
// Use .RELEASE or -SNAPSHOT suffix after version code
version = '0.0.5-SNAPSHOT'

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'

    apply from: "$devConfRoot/configs/dependencies.gradle"
    apply from: "$devConfRoot/configs/quality/codeQuality.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = rootProject.group
    version = rootProject.version

    java {
        withSourcesJar()
    }

    dependencies {
        annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:${versions.spring_boot}")
             implementation platform("org.springframework.boot:spring-boot-dependencies:${versions.spring_boot}")

        annotationProcessor libraries.lombok,
                            libraries.spring_boot_configuration_processor

                compileOnly libraries.lombok,
                            libraries.spring_boot_starter_logging
    }

    // spring boot configuration processor only build meta json into build folder
    // but auto complete needs it in src, so do copy after compile java task
    task cpMetaToSrc(type: Copy) {
        from 'build/classes/java/main/META-INF'
        into 'src/main/resources/META-INF'
    }
    compileJava.finalizedBy cpMetaToSrc

    publishing {
        repositories {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/AlphaHinex/spring-roll')
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GH_OWNER")
                    password = project.findProperty("gpr.key") ?: System.getenv("GH_PKG_ADMIN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}